apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jetbrains.kotlin.android.extensions'

ext.majorVersion = 1
ext.minorVersion = 0
ext.patchVersion = 0
ext.preRelease = ""
ext.minSdkVersion = 21
ext.targetSdkVersion = 27

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.jati.dev.movielist"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        project.ext.set("archivesBaseName", "OMDB-MovieList-" + generateVersionName())
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    androidExtensions {
        experimental = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintVersion"
    implementation "com.android.support:design:$supportVersion"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$rxBindingVersion"

    // network
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp3Version"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$retrofitAdapter"
    implementation "com.google.code.gson:gson:$gsonVersion"

    //image
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // DI
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

private Integer generateVersionCode() {
    return ext.minSdkVersion * 1000000 + ext.majorVersion * 10000 + ext.minorVersion * 100 + ext.patchVersion
}

private String generateVersionName() {
    String versionName = "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
    if (!ext.preRelease.isEmpty()) {
        versionName = versionName + "-" + ext.preRelease
    }
    return versionName
}

